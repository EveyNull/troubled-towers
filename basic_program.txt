10 GOSUB 1900
25 M$=""
30 HOME
33 PRINT M$
35 PRINT "------------"
40 PRINT "YOUR LOCATION:"
50 PRINT LOCS$(RM)
60 PRINT "EXITS:"
70 FOR I=1 TO LEN(ROUTES$(RM))
80 PRINT MID$(ROUTES$(RM),I,1);",";
90 NEXT I
95 PRINT ""
100 FOR I=1 TO O
110 IF OBJLOCS(I)=RM THEN PRINT "I SEE ";OBJS$(I);" HERE"
120 NEXT I
135 M$="WHAT?"
140 PRINT "PLEASE TYPE SOMETHING"
145 INPUT Q$
150 VERB$=""
155 WORD$=""
160 FOR I=1 TO LEN(Q$)
165 IF MID$(Q$,I,1)=" " AND VERB$="" THEN VERB$=LEFT$(Q$,I-1)
170 IF MID$(Q$,I+1,1)<>" " AND VERB$<>"" THEN WORD$=MID$(Q$,I+1,LEN(Q$)-1):I=LEN(Q$)
180 NEXT I
190 IF WORD$="" THEN VERB$=Q$
250 VB=0
255 FOR I=1 TO V
260 IF VERB$=VERBS$(I) THEN VB=I
265 NEXT I
270 OB=0
275 FOR I=1 TO O
280 IF WORD$=OBJS$(I) THEN OB=I
285 NEXT I
300 IF WORD$>"" AND OB=1 THEN M$="THAT'S SILLY"
305 IF WORD$="" THEN M$="I NEED TWO WORDS"
310 IF VB=1 AND OB>1 THEN M$="YOU CAN'T '"+Q$+"'"
320 IF VB=1 AND OB=1 THEN M$="YOU DON'T MAKE SENSE"
330 IF VB>1 AND OB>1 AND INVENTORY(OB)<>1 THEN M$="YOU DON'T HAVE "+OBJS$(OB)
340 IF FLAGS(7)=0 AND RM=1 THEN M$="THE DEMON IS ATTACKING!"
350 ON VB GOSUB 370,430,471,0,504,504,504,504,504,504,504,640,640,670,685,705,730,750,790,805,820,835,855,875
360 GOTO 30
370 M$="WORDS I RECOGNISE: "
380 FOR I=1 TO V
390 M$= M$+VERBS$(I)+","
400 NEXT I
420 RETURN
430 M$="YOU ARE CARRYING: "
440 FOR I=1 TO G
450 IF INVENTORY(I)=1 THEN M$=M$+OBJS$(I)+","
460 NEXT I
470 RETURN
471 IF OB=1 AND FLAGS(1)=0 THEN M$="THE CANDLES ARE UNLIT"
472 IF OB=1 AND FLAGS(1)=1 THEN M$="THE CANDLES GIVE OFF A DIM LIGHT"
473 IF OB=2 THEN M$="A BOX OF MATCHES"
474 IF OB=3 THEN M$="A SET OF SHINY, CLEAN PLATES"
475 IF OB=4 THEN M$="A HOLY CRUCIFIX"
476 IF OB=5 AND FLAGS(10)=0 THEN M$="AN EMPTY GLASS"
477 IF OB=5 AND FLAGS(10)=1 THEN M$="A GLASS OF REGULAR TAP WATER"
478 IF OB=5 AND FLAGS(11)=1 THEN M$="A GLASS FULL OF BLESSED HOLY WATER"
479 IF OB=6 THEN M$="A CHILD'S DOLL, CUTE!"
480 IF OB=7 THEN M$="A RED RIBBON BOW, IT WOULD LOOK NICE ON A TOY DOLL"
481 IF OB=8 THEN M$="IT GIVES OFF A DIVINE AURA"
482 IF OB=9 AND FLAGS(4)=0 THEN M$="CREEPY WRITING COVERS THE NOTE"
483 IF OB=9 AND FLAGS(4)=1 THEN M$="'IN THE DEEPEST DARK, PRESS ONWARDS'":FLAGS(5)=1:ROUTES$(2)="EW"
484 IF OB=10 THEN M$="A LOVELY CLEAN TOWEL"
485 IF OB=17 THEN M$="IT HOVERS IN THE AIR. IT LOOKS UNHAPPY"
486 IF OB=18 THEN M$="IT'S ATTACKING YOU! FIND SOME WAY TO DEFEND YOURSELF!"
487 IF OB=19 AND FLAGS(8)=0 THEN M$="A TOILET. WHAT ELSE?"
488 IF OB=19 AND FLAGS(8)=1 THEN M$="URGH! IT'S CLOGGED WITH SOMETHING. SMELLS AWFUL!"
489 IF OB=20 AND FLAGS(3)=0 THEN M$="A FIREPLACE. IT'S NOT LIT"
490 IF OB=20 AND FLAGS(3)=1 THEN M$="A WARM FIRE LIGHTS UP THE ROOM"
491 IF OB=21 AND FLAGS(9)=0 THEN M$="JUST A PLAIN BATHROOM SINK"
492 IF OB=21 AND FLAGS(9)=1 THEN M$="WATER TRICKLES STEADILY FROM THE SINK"
494 IF OB=22 THEN M$="A WOODEN DESK WITH A DRAWER"
495 IF OB=23 THEN M$="A SMALL GHOST THAT LOOKS LIKE A CHILD"
502 IF OB>0 AND OBJLOC(OB) <> RM AND INVENTORY(OB)=0 THEN M$="YOU SEE NO "+OBJS$(OB)+" HERE"
503 RETURN
504 D=0
505 IF OB=0 THEN D=VB-5
510 IF OB=11 THEN D=1
515 IF OB=12 THEN D=2
520 IF OB=13 THEN D=3
525 IF OB=14 THEN D=4
530 IF OB=15 THEN D=5
540 IF OB=16 THEN D=6
545 IF D=5 AND RM=3 THEN D=3
550 IF D=6 AND RM=4 THEN D=4
555 IF D=5 AND RM=22 THEN D=3
560 IF D=6 AND RM=23 THEN D=4
565 IF RM=1 AND FLAGS(7)=0 THEN M$="THE DEMON BLOCKS YOUR EXIT!":RETURN
570 IF RM=4 AND D=4 AND FLAGS(1)=0 THEN M$="THE CELLAR LOOKS TOO DARK TO NAVIGATE":RETURN
575 IF RM=28 AND D=2 AND FLAGS(6)=0 THEN M$="A SUPERNATURAL FORCE HOLDS THE DOOR SHUT":RETURN
578 IF RM=28 AND D=2 AND FLAGS(6)=1 THEN PRINT "CONGRATULATIONS! YOU ESCAPED!":END
580 LET A=0:RL=LEN(ROUTES$(RM))
585 FOR I=1 TO RL
590 U$=MID$(ROUTES$(RM),I,1)
595 IF(U$="N" AND D=1 AND A=0) THEN RM=RM-6:A=1
600 IF(U$="S" AND D=2 AND A=0) THEN RM=RM+6:A=1
605 IF(U$="E" AND D=3 AND A=0) THEN RM=RM+1:A=1
610 IF(U$="W" AND D=4 AND A=0) THEN RM=RM-1:A=1
615 NEXT I
620 M$="OKAY"
625 IF A=0 THEN M$="YOU CAN'T GO THAT WAY!"
630 IF D<1 THEN M$="GO WHERE?"
635 RETURN
640 IF OBJLOC(OB) <> RM THEN M$="YOU DON'T SEE THAT HERE"
645 IF INVENTORY(OB)=1 THEN M$="YOU ALREADY HAVE THAT!"
646 IF RM=5 AND OBJ=6 AND FLAGS(12)=0 THEN M$="IT SMELLS TOO BAD TO GET CLOSE!":RETURN
650 IF OB>0 AND OBJLOC(OB) = RM AND OB < G+1 THEN INVENTORY(OB)=1:OBJLOC(OB)=65:M$="GOT IT!"
655 IF OB>G THEN M$="YOU CAN'T PICK THAT UP!"
660 RETURN
670 IF OB>0 THEN M$="YOU CAN'T OPEN THAT"
675 IF OB = 22 AND RM = 18 AND OBJLOC(9) = 6 THEN M$="YOU FIND A SPOOKY LOOKING NOTE IN THE DESK!":INVENTORY(9)=1
680 RETURN
685 M$="YOU CAN'T WEAR THAT!"
690 IF OB = 10 AND INVENTORY(10)=1 THEN FLAGS(12)=1:M$="YOU WRAP THE TOWEL AROUND YOUR MOUTH AS A MASK"
695 IF OB = 8 AND INVENTORY(8)=1 THEN FLAGS(13)=1:M$="YOU HANG THE AMULET AROUND YOUR NECK"
700 RETURN
705 M$="YOU CAN'T LIGHT THAT ON FIRE"
710 IF INVENTORY(2)=0 THEN M$="LIGHT A FIRE WITH WHAT?"
715 IF OB=1 AND INVENTORY(1)=1 AND INVENTORY(2)=1 THEN FLAGS(1)=1:M$="YOU LIGHT THE CANDLES"
720 IF OB=20 AND RM=20 THEN FLAGS(3)=1:M$="YOU LIGHT THE FIREPLACE"
725 RETURN
730 M$="FLUSH WHAT?"
735 IF RM=5 AND FLAGS(8)=1 THEN M$="THE TOILET IS ALREADY BLOCKED. BETTER NOT MAKE IT WORSE"
740 IF RM=5 AND FLAGS(8)=0 THEN FLAGS(8)=1:OBJLOC(6)=5:M$="YOU FLUSH THE TOILET. IT GURGLES AND CHURNS AND A DOLL COMES UP IN THE MURKY WATER"
745 RETURN
750 IF RM<>1 OR OB<>18 THEN M$="YOU ATTACK AT NOTHING"
755 IF RM=1 AND OB=18 THEN M$="YOU'RE POWERLESS AGAINST A DEMON!"
760 IF RM=1 AND OB=18 AND FLAGS(13)=0 THEN M$="YOU CAN'T RISK IT WITHOUT PROTECTION!"
772 IF RM=1 AND OB=18 AND FLAGS(13)=1 THEN M$="NOTHING YOU HAVE IS EFFECTIVE!"
775 IF RM=1 AND OB=18 AND FLAGS(13)=1 AND FLAGS(11)=1 AND INVENTORY(5)=1 THEN FLAGS(7)=1:OBJLOC(7)=1:OBJLOC(18)=6:M$="YOU THROW THE HOLY WATER OVER THE DEMON. IT SCREECHES IN PAIN AND TWISTS UP AND DIES. IT DROPS SOMETHING!"
780 IF RM=17 OR RM=24 THEN M$="IT'S ALREADY DEAD!"
785 RETURN
790 M$="NO WAY TO BURN THAT"
795 IF RM=20 AND OB=9 AND FLAGS(3)=1 THEN FLAGS(4)=1:M$="YOU LIGHT THE SPOOKY NOTE ON FIRE. IT SMOLDERS BUT DOESN'T BURN ENTIRELY. THE MESSAGE CHANGES!"
800 RETURN
805 M$="USE THAT HOW?"
810 IF RM=29 AND OB=21 THEN FLAGS(9)=1:M$="YOU TURN THE SINK ON"
815 RETURN
820 M$="FILL THAT HOW?"
825 IF RM=29 AND FLAGS(9)=1 THEN FLAGS(10)=1:M$="YOU FILL THE GLASS WITH MURKY WATER"
830 RETURN
835 M$="YOU CAN'T DRESS THAT"
840 IF INVENTORY(6)=1 THEN M$="YOU HAVE NOTHING TO DRESS IT WITH"
845 IF INVENTORY(6)=1 AND INVENTORY(7)=1 THEN FLAGS(2)=1:INVENTORY(7)=0:M$="YOU DRESS THE DOLL WITH THE PRETTY BOW. LOVELY!"
850 RETURN
855 IF OB<>5 OR FLAGS(10)=0 THEN M$="YOU CAN'T BLESS THAT"
860 IF INVENTORY(4)=0 THEN M$="YOU HAVE NOTHING TO BLESS IT WITH"
865 IF OB=5 AND FLAGS(10)=1 AND INVENTORY(4)=1 THEN FLAGS(11)=1:M$="YOU DIP THE CRUCIFIX IN THE GLASS OF WATER. IT PURIFIES AND CLEARS!"
870 RETURN
875 IF OB<>6 THEN M$="NO ONE WOULD WANT THAT AS A GIFT!"
880 IF RM=1 OR RM=17 THEN M$="IT DOESN'T WANT IT"
885 IF RM=24 AND OB=6 AND FLAGS(2)=0 THEN M$="IT LOOKS AT THE DOLL BUT LOOKS AWAY AGAIN. SOMETHING'S NOT QUITE RIGHT."
890 IF RM=24 AND OB=6 AND FLAGS(2)=1 THEN FLAGS(6)=1:OBJLOC(17)=6:OBJLOC(23)=6:M$="IT LOOKS AT THE DOLL. SUDDENLY IT SMILES. THE GHOST AND THE DOLL FADE AWAY SUDDENLY! YOU FEEL A WEIGHT LIFT OFF THE HOUSE. A DOOR SWINGS OPEN SOMEWHERE NEARBY"
895 RETURN
1900 PRINT ""
1910 LET L=30
1920 LET O=23
1930 LET G=10
1940 LET V=24
2000 LET RM=28
2100 DIM LOCS$(L)
2110 DATA RITUAL CHAMBER,CELLAR (W),CELLAR (E),BACK CORRIDOR,TOILET,NONE,NONE,PANTRY,NONE,BRIGHT CORRIDOR,NONE,NONE,DINING TABLE,KITCHEN (W),KITCHEN (E),MOULDY CORRIDOR,MASTER BEDROOM,STUDY,GAMES ROOM,LOUNGE,SIDE ROOM,SMALL STAIRCASE,LANDING,SMALL BEDROOM,NONE,NONE,NONE,FRONT DOOR,BATHROOM,SHOWER
2120 FOR I=1 TO L
2130 READ LOCS$(I)
2150 NEXT I
2200 DIM ROUTES$(L)
2210 DATA E,E,EWU,SEWD,W,,,S,,NS,,,SE,NEW,EW,NSW,SE,W,NE,EW,EW,NSEWU,NSEWD,W,,,,NS,NE,W
2220 FOR I=1 TO L
2230 READ ROUTES$(I)
2250 NEXT I
2300 DIM OBJS$(O)
2310 DATA CANDLES,MATCHES,PLATES,CRUCIFIX,GLASS,DOLL,DOLL BOW,HOLY AMULET,SPOOKY NOTE,TOWEL,NORTH,SOUTH,EAST,WEST,UP,DOWN,GHOST,DEMON,TOILET,FIREPLACE,SINK,DESK,SMALL GHOST
2320 FOR I=1 TO O
2330 READ OBJS$(I)
2350 NEXT I
2400 DIM VERBS$(V)
2410 DATA HELP,I,EXAMINE,SCORE,GO,N,S,E,W,U,D,GET,TAKE,OPEN,WEAR,LIGHT,FLUSH,ATTACK,BURN,USE,FILL,DRESS,BLESS,GIVE
2420 FOR I=1 TO V
2430 READ VERBS$(I)
2450 NEXT I
2500 DIM OBJLOC(O)
2510 DATA 19,18,13,10,13,6,6,20,6,30,6,6,6,6,6,6,17,1,5,20,29,18,24
2520 FOR I=1 TO O
2530 READ OBJLOC(I)
2540 NEXT I
2600 DIM INVENTORY(O)
2700 DIM FLAGS(13)
2800 RETURN